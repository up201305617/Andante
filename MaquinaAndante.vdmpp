class MaquinaAndante

-- A classe CartaoAndante permite representar a MaquinaAndante
-- onde se pode adquirir e carregar titulos de viagem Andante.

types

	public Password = seq of char;
	public SacoDinheiro = Dinheiro`BagOfCoins;
	public Status = <OnFactory> | <OnMaintenance> | <Idle> | <EnteringCoins> | <Delivering>;

instance variables

	----------------------------------------------------------------
	-- VARIAVEIS DE STOCK
	----------------------------------------------------------------
	public quantidadeCartoes: nat := 0;	-- Stock de titulos Andante
	public quantidadeRecibo: nat := 0; -- Stock de papel de recibos
	public stockDinheiro: SacoDinheiro := {|->}; -- Stock de moedas
	
 	------------------------------------------------------------------------------------
	-- VARIAVEIS DAS OPERACOES
	-----------------------------------------------------------------------------------
	public quantiaInserida: nat := 0; -- Quantia total inserida na maquina (centimos)
	public andante : [CartaoAndante] := nil; -- Titulo a ser comprado/carregado
	public recibo : [Recibo] := nil; -- Recibo a ser processado
	public troco : SacoDinheiro := {|->};  -- Quantia a devolver ao cliente
	
	------------------------------------------------------------------
	-- VARIAVEIS DO FABRICANTE
	------------------------------------------------------------------
	public codigo : Password := [];  -- Password do fabricante
	public status : Status := <OnFactory>; -- Estado atual da maquina
	
	---------------------------------------
	-- ZONAS DE MOBILIDADE ANDANTE
	---------------------------------------
	public z2: Zona := new Zona("z2",120);
	public z3: Zona := new Zona("z3",155);
	public z4: Zona := new Zona("z4",195);
	public z5: Zona := new Zona("z5",235);
	public z6: Zona := new Zona("z6",275);
	public z7: Zona := new Zona("z7",315);
	public z8: Zona := new Zona("z8",355);
	public z9: Zona := new Zona("z9",395);
	public z10: Zona := new Zona("z10",435);
	public z11: Zona := new Zona("z11",475);
	public z12: Zona := new Zona("z12",515);
	
operations
	------------------------------------------------------------------------
	-- OPERACOES DO FABRICANTE
	------------------------------------------------------------------------
	
	-- Construtor da classe
	public MaquinaAndante: () ==> MaquinaAndante
		MaquinaAndante() == 
			return self;
	
	-- Define a password da maquina
	public definirPassword: Password ==> ()
	definirPassword(code) ==
		codigo := code
	pre status = <OnFactory>;
	
	-- Prepara a maquina para ser utilizada
	public sairFabrica: () ==> ()
	sairFabrica() ==
		status := <Idle>
	pre status = <OnFactory>;
	------------------------------------------------------------------------
	
	------------------------------------------------------------------------
	-- OPERACOES DE MANUTENCAO
	------------------------------------------------------------------------
	
	-- Abre a maquina para efetuar operacoes de manutencao
	public abrirMaquina: Password ==> ()
	abrirMaquina(code) ==
		if code = codigo then 
			status := <OnMaintenance>
	pre status = <Idle>;
	
	-- Restaura os stocks de moedas, titulos e papel de recibo
	public carregarMaquina: SacoDinheiro * nat1 * nat1 ==> () 
	carregarMaquina(coins,cartoes,papel) == (
		stockDinheiro := coins;
		quantidadeCartoes := cartoes;
		quantidadeRecibo := papel
	)
	pre status = <OnMaintenance>; 
	
	-- Termina operacoes de manutencao
	public fecharMaquina: () ==> ()
	fecharMaquina() ==
		status := <Idle>
	pre status = <OnMaintenance>;
	
	-- Reinicia a maquina
	public reset: () ==> ()
	reset() ==
		status := <Idle>;
	------------------------------------------------------------------------
	
	------------------------------------------------------------------------
	-- OPERACOES CLIENTE
	------------------------------------------------------------------------
	
	-- Muda a zona associada ao titulo Andante
	public mudarZona: Zona * CartaoAndante ==> ()
		mudarZona(z,c) == (
			c.mudarZona(z);
			andante := c;
			status := <Idle>;
		)
		pre status = <Idle>
		post quantidadeCartoes = quantidadeCartoes~;
	
	-- Insere dinheiro na maquina
	public inserirDinheiro: Dinheiro`Coin ==> () 
	inserirDinheiro(coin) == 
		atomic (
			stockDinheiro := Dinheiro`add(coin, stockDinheiro);
			quantiaInserida :=  quantiaInserida + coin;
			status := <EnteringCoins>
		)
	pre status in set {<Idle>, <EnteringCoins>}; 
	
	-- Recolhe o troco da maquina
	public apanharTroco: () ==> SacoDinheiro 
	apanharTroco() ==
		let r = troco in (
			atomic( 
				troco := {|->};
				status := if andante = nil then <Idle> else <Delivering>
			);
			return r
		)
	pre troco <> {|->};
	
	-- Recolhe titulo Andante da maquina
	public apanharAndante: () ==> CartaoAndante 
	apanharAndante() == (
		let r = andante in (
			atomic( 
				andante := nil;
				status := if andante = nil then <Idle> else <Delivering>
			);
			return r
		)
	)
	pre andante <> nil;
	
	-- Recolhe papel de recibo da maquina
	public apanharRecibo: () ==> Recibo 
	apanharRecibo() == (
		let r = recibo in (
			atomic( 
				recibo := nil;
				status := if recibo = nil then <Idle> else <Delivering>
			);
			return r
		)
	)
	pre recibo <> nil;
	
	-- Cancela a compra/carregamento
	public cancelar: () ==> ()
  cancelar() ==
		let chg = Dinheiro`extract(stockDinheiro, quantiaInserida) in 
			atomic (
				stockDinheiro := Dinheiro`removeAll(chg, stockDinheiro);
				quantiaInserida := 0;
				troco := chg;
				status := <Delivering>
			)
	pre status = <EnteringCoins>;  
	
	-- Compra um novo titulo Andante carregado
	public comprarAndante: nat1 * Zona * bool ==> ()
	comprarAndante(q, z, r) == 
		let cartao = new CartaoAndante(q,z),
		preco = (cartao.zona.preco * cartao.quantidade) + 60,
		chg = Dinheiro`extract(stockDinheiro, quantiaInserida - preco) in (
		  		atomic (
						stockDinheiro := Dinheiro`removeAll(chg, stockDinheiro);
						quantidadeCartoes := quantidadeCartoes - 1;
						quantiaInserida := 0;
						troco := chg; 
						andante := cartao;
						status := <Delivering>
				);
				
				if r then 
				(
					quantidadeRecibo := quantidadeRecibo - 1;
					recibo := new Recibo(q,z,"Compra");
				)
	)
	pre status = <EnteringCoins>  
	    and quantidadeCartoes > 0
			and quantiaInserida >= z.preco * q
			and quantidadeRecibo > 0
	    and Dinheiro`canExtract(stockDinheiro, quantiaInserida - z.preco * q);
	
	-- Carrega um titulo Andante previamente adquirido
	public carregarAndante: nat * CartaoAndante * bool ==> ()
	carregarAndante(q, c, r) ==(
		c.carregarAndante(q);
		let preco = c.zona.preco * q,
		chg = Dinheiro`extract(stockDinheiro, quantiaInserida - preco) in (
		  		atomic (
						stockDinheiro := Dinheiro`removeAll(chg, stockDinheiro);
						quantiaInserida := 0;
						troco := chg; 
						andante := c;
						status := <Delivering>
				);
				
				if r then 
				(
					quantidadeRecibo := quantidadeRecibo - 1;
					recibo := new Recibo(q,c.zona,"Carregamento");
				)
				
	))
	pre status = <EnteringCoins>  
			and quantiaInserida >= c.zona.preco * q
			and quantidadeRecibo > 0
	    and Dinheiro`canExtract(stockDinheiro, quantiaInserida - c.zona.preco * q);
	
end MaquinaAndante
